# ------------------------------------------------------------------------------
#        Multi primitive-to-primitive (MP2P) ICP C++ library
#
# Copyright (C) 2018-2020, Jose Luis Blanco-Claraco, contributors (AUTHORS.md)
# All rights reserved.
# Released under BSD 3-Clause License. See COPYING.
# ------------------------------------------------------------------------------

# Minimum CMake vesion: limited by CMAKE_WINDOWS_EXPORT_ALL_SYMBOLS
cmake_minimum_required(VERSION 3.4)

# Tell CMake we'll use C++ for use in its tests/flags
project(mp2p_icp LANGUAGES CXX)

# 3rd party:
add_subdirectory(3rdparty)

# find dependencies:
find_package(mrpt-containers 2.0.5 REQUIRED)
find_package(mrpt-tfest REQUIRED)
find_package(mrpt-maps REQUIRED)
find_package(mrpt-gui REQUIRED) # for tests/demos

# MOLA CMake scripts: "mola_xxx()"
# to allow stand-along builds outside of MOLA, use local copy instead:
find_package(mola-common QUIET)
if (NOT mola-common_FOUND)
	message(STATUS "Standalone ${PROJECT_NAME} build: cloning required mola-common repository...")
	execute_process(COMMAND git clone git@github.com:MOLAorg/mola-common.git "${CMAKE_CURRENT_BINARY_DIR}/mola-common-src")
	execute_process(COMMAND "${CMAKE_COMMAND}" -S "${CMAKE_CURRENT_BINARY_DIR}/mola-common-src" -B "${CMAKE_CURRENT_BINARY_DIR}/mola-common-build")
	# 2nd attempt:
	set(mola-common_DIR "${CMAKE_BINARY_DIR}/mola-common-build")
	find_package(mola-common REQUIRED)
endif()

# -----------------------
# define lib:
file(GLOB_RECURSE LIB_SRCS src/*.cpp src/*.h)
file(GLOB_RECURSE LIB_PUBLIC_HDRS include/*.h)

mola_add_library(
	TARGET ${PROJECT_NAME}
	SOURCES ${LIB_SRCS} ${LIB_PUBLIC_HDRS}
	PUBLIC_LINK_LIBRARIES
		mrpt::containers
		mrpt::tfest
		mrpt::maps
		mrpt::gui
)

if (MP2PICP_BUILD_POINTMATCHER)
	target_compile_definitions(${PROJECT_NAME} PRIVATE MP2P_HAS_LIBPOINTMATCHER)
	target_link_libraries(${PROJECT_NAME} PRIVATE libpointmatcher::libpointmatcher)
endif()


# -----------------------
# define tests:
enable_testing()

add_subdirectory(tests)
